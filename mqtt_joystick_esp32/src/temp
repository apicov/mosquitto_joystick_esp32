/**
 * @file temp
 * @brief Alternate/test firmware for ESP32 joystick MQTT publisher.
 */
#include <Arduino.h>
#include <PubSubClient.h>
#include <WiFi.h>

#include "private_data.h"

/**
 * @brief Built-in LED pin number for ESP32.
 */
int LED_BUILTIN = 2;

/**
 * @brief Pin number for the red button.
 */
#define RED_BUTTON 34
/**
 * @brief Pin number for the up direction.
 */
#define UP 35
/**
 * @brief Pin number for the down direction.
 */
#define DOWN 32
/**
 * @brief Pin number for the left direction.
 */
#define LEFT 33
/**
 * @brief Pin number for the right direction.
 */
#define RIGHT 25

/**
 * @brief Number of joystick pins.
 */
#define N_PINS 5
/*
#define SSID "ssid"
#define PASSWORD "password"

const char* mqtt_broker = "ip";
const int mqtt_port = port_n;
*/


/**
 * @brief MQTT client for ESP32.
 */
WiFiClient espClient;
/**
 * @brief PubSub MQTT client using espClient.
 */
PubSubClient client(espClient);

/**
 * @brief MQTT topic for joystick commands.
 */
const char* mqtt_topic = "xcarve_ctl/joystick/cmd";
/**
 * @brief Array of joystick command strings.
 */
const char* joystick_cmds[] = {"Button.red", "Key.up", "Key.down", "Key.left", "Key.right"};
/**
 * @brief Counter variable (unused).
 */
int cont  = 0;


/**
 * @brief Array of joystick pin numbers.
 */
const uint8_t joystick_pins[N_PINS] = {RED_BUTTON, UP, DOWN, LEFT, RIGHT};
/**
 * @brief Array holding the status of each joystick pin.
 */
uint8_t joystick_status[N_PINS] = {0,0,0,0,0};
/**
 * @brief Reads the status of the joystick pins and updates the status array.
 * @param status_array Pointer to the array to update with pin statuses.
 */
void read_joystick(uint8_t* status_array);
/**
 * @brief Prints the status array to the serial monitor.
 * @param array Pointer to the array to print.
 */
void print_array(uint8_t* array);


/**
 * @brief Connects to the MQTT broker.
 */
void mqtt_connect();
/**
 * @brief Initializes the WiFi connection.
 */
void initWiFi();

/**
 * @brief Arduino setup function. Initializes serial, pins, WiFi, and MQTT.
 */
void setup() {

  Serial.begin(9600);

  pinMode (LED_BUILTIN, OUTPUT);

  pinMode(RED_BUTTON, INPUT );
  pinMode(UP, INPUT );
  pinMode(DOWN, INPUT );
  pinMode(LEFT, INPUT );
  pinMode(RIGHT, INPUT );


  // Connect to Wi-Fi
  /*WiFi.begin(SSID, PASSWORD);
  while (WiFi.status() != WL_CONNECTED) {
      delay(1000);
      Serial.println("Connecting to WiFi..");
  }
  Serial.println("Connected to the WiFi network");*/

  initWiFi();
  Serial.println("Connected to the WiFi network");

  // Set the MQTT server to the client
  client.setServer(mqtt_broker, mqtt_port);
  
  mqtt_connect();
}

/**
 * @brief Arduino main loop. Handles WiFi/MQTT reconnection and joystick command publishing.
 */
void loop() {

  //connect to network in case there is no conection
    if (WiFi.status() != WL_CONNECTED){
      initWiFi();
      Serial.print("wifi connected.");
    }
  
  if (!client.connected()) {
    mqtt_connect();
  }

  read_joystick(joystick_status);
  //print_array(joystick_status);

  //check joystick status, publish first one found in array

  //check if button was pressed
  if (joystick_status[0] == 1){
    //publish message
    client.publish(mqtt_topic, joystick_cmds[0]);
    delay(5000); //wait a while for avoiding  mechanical bouncing of button
  }
  else { //check the others
    for(int i=1; i<N_PINS; i++){
      if (joystick_status[i] == 1){
        // Publish a message
        client.publish(mqtt_topic, joystick_cmds[i]);
        Serial.print(joystick_cmds[i]);
        break;
      }
    }
  }
 delay(70);

}


/**
 * @brief Connects to the MQTT broker, retrying until successful.
 */
void mqtt_connect() {
  // Connect to MQTT Broker
  while (!client.connected()) {
      Serial.println("Connecting to MQTT Broker...");
      if (client.connect("ESP32Client")) {
          Serial.println("Connected to MQTT Broker");
      } else {
          Serial.print("Failed with state ");
          Serial.print(client.state());
          delay(5000);
      }
  }
}


/**
 * @brief Reads the status of the joystick pins and updates the status array.
 * @param status_array Pointer to the array to update with pin statuses.
 */
void read_joystick(uint8_t* status_array){
  for(int i=0; i<N_PINS; i++ ){
    status_array[i] = !digitalRead(joystick_pins[i]);
  }

}

/**
 * @brief Prints the status array to the serial monitor.
 * @param array Pointer to the array to print.
 */
void print_array(uint8_t* array){
  for(int i=0; i<N_PINS; i++){
    Serial.print(array[i]);
    Serial.print(" ");
  }
  Serial.println("");
}


/**
 * @brief Initializes the WiFi connection and waits until connected.
 */
void initWiFi() {
  WiFi.mode(WIFI_STA);
  WiFi.begin(SSID, PASSWORD);
  Serial.print("Connecting to WiFi ..");
  while (WiFi.status() != WL_CONNECTED) {
    digitalWrite(LED_BUILTIN, HIGH);
    Serial.print('.');
    delay(500);
    digitalWrite(LED_BUILTIN, LOW);
    delay(500);
  }
  Serial.println(WiFi.localIP());
}
